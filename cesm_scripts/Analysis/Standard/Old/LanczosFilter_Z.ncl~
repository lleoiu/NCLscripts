load "$NCARG_ROOT/lib/ncarg/nclex/gsun/gsn_code.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/gsn_csm.ncl"
load "$NCARG_ROOT/lib/ncarg/nclscripts/csm/contributed.ncl"

begin
;Experiments = (/"CESMtopof19","CESMnotopof19","CESMnoRTf19","CESMnoRf19","CESMnoTf19","CESMnoT2f19","CESMnoT3f19","CESMnoRT2f19"/)
Experiments = (/"CESMnoT3f19","CESMnoRT2f19"/)

nyears = 40

print(Experiments)

dimsexps = dimsizes(Experiments)
nexps = dimsexps(0)

do exps = 0,nexps-1

Experiment = Experiments(exps)

print(Experiment)

iyear = 2
yearstring = ("0" + tostring(iyear))

filename = ("/home/disk/eos4/rachel/CESM_outfiles/" + Experiment + "/atm/hist/" + Experiment + ".cam2.h1.00" + yearstring +"-01-01-00000.nc")

cdf_file = addfile(filename,"r")
print("file read in")

lat = cdf_file->lat
lon = cdf_file->lon
Z850 = cdf_file->Z850

time  = cdf_file->time
  
ntim  = dimsizes(Z850)
print(ntim)   

do iyear = 3,nyears +1
	print(iyear)
	if iyear .lt. 10
		yearstring = ("0" + tostring(iyear))
	else
		yearstring = tostring(iyear)
	end if

	filename = ("/home/disk/eos4/rachel/CESM_outfiles/" + Experiment + "/atm/hist/" + Experiment + ".cam2.h1.00" + yearstring +"-01-01-00000.nc")

	cdf_file = addfile(filename,"r")
	print("file read in")

	Z850_temp = cdf_file->Z850

	time_temp  = cdf_file->time
  
	Z850_temp2 = array_append_record(Z850,Z850_temp,0)
	time_temp2 = array_append_record(time,time_temp,0)
	delete(Z850)
	delete(time)
	Z850 = Z850_temp2
	time = time_temp2
	delete(Z850_temp2)
	delete(time_temp2)

end do

ntim  = dimsizes(Z850)
print(ntim)   


; ***********************************************
; create the filter weights and apply
; ***********************************************
  ihp     = 2                             ; band pass
  sigma   = 1.0                           ; Lanczos sigma

  nWgt    = 1461                           ; 1461 lose half a year at each end = 2*740+1 = 1461  

  dt = 0.25                                ; days per timestep
  
  fca     = dt/6.                        ; low freq, 6 days = 1/24 timesteps
  fcb     = dt/2.                        ; high freq = 2 days = 1/8 timesteps

print(fca)
print(fcb)
  wgt     = filwgts_lanczos (nWgt, ihp, fca, fcb, sigma )

 Z850bpf_temp = Z850
 Z850bpf_temp(:,:,:) = 0.0

do ilat = 0,ntim(1)-1
	print(ilat * 1)
	do ilon = 0,ntim(2)-1
		Z850bpf_temp(:,ilat,ilon) = wgt_runave (Z850(:,ilat,ilon), wgt, 0 )      ;
	end do
end do



ndims = dimsizes(Z850bpf_temp)
ntimes = ndims(0)
print(ndims)

Z850bpf_temp!0 = Z850!0
Z850bpf_temp!1 = Z850!1
Z850bpf_temp!2 = Z850!2
delete(Z850)

nDJF = nyears*4*(31+28+31);
Z850bpf = new((/nDJF,ndims(1),ndims(2)/),typeof(Z850bpf_temp))
timebpf = new((/nDJF/),typeof(time))

count = 0

do nt = 731,ntimes-731
	days = mod(time(nt),365)
	if (days .LT. 60 .OR. days .GT. 333)
		Z850bpf(count,:,:) = Z850bpf_temp(nt,:,:)
		timebpf(count) = time(nt)
		count = count + 1
	end if
end do

Z850bpf!0 = Z850bpf_temp!0
Z850bpf!1 = Z850bpf_temp!1
Z850bpf!2 = Z850bpf_temp!2

delete(Z850bpf_temp)
ndims2 = dimsizes(Z850bpf(0:count-1,:,:))
print(ndims2)
;-----------------------------------------------------------------------
;Write variables to a new netcdf file, for stat fits in matlab
;-----------------------------------------------------------------------
diro = "/home/disk/eos4/rachel/CESM_outfiles/" + Experiment +"/atm/hist/"
filo = "Z850_bpf_DJF_" + tostring(nyears) + "_" + Experiment + ".cam2.h1.nc"
system("/bin/rm -f " + diro + filo)
fout = addfile(diro + filo, "c")

setfileoption(fout,"DefineMode",True)

; set attributes of output file
fAtt = True
fAtt@creation_date = systemfunc("date")
fileattdef(fout,fAtt)

;set dimensions of output file
dimNames = (/"time","lat","lon"/)
dimSizes = (/ndims2(0),ndims2(1),ndims2(2)/)
dimUnlim = (/True,False,False/)
filedimdef(fout,dimNames,dimSizes,dimUnlim)

filevardef(fout,"time",typeof(timebpf),getvardims(timebpf))
filevardef(fout,"lat",typeof(lat),getvardims(lat))
filevardef(fout,"lon",typeof(lon),getvardims(lon))

filevardef(fout,"Z850bpf",typeof(Z850bpf),getvardims(Z850bpf))

fout ->time    = (/timebpf/)
fout ->lat    = (/lat/)
fout ->lon    = (/lon/)

fout ->Z850bpf = (/Z850bpf/)

delete(Z850bpf)
delete(timebpf)
delete(lat)
delete(lon)
delete(Z850_temp)
delete(time_temp)
delete(time)
end do

end